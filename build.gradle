buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.bmuschko:gradle-tomcat-plugin:2.2.2"
        classpath "org.flywaydb:flyway-gradle-plugin:3.2.1"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'application'
apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'org.flywaydb.flyway'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '1.0'

war.baseName = "simpleSpring"

war {
    //this is for the database configuration (-Penvironment=local, -Penvironment=snap, -Penvironment=heroku)
    eachFile {
        if (it.name == 'applicationContext.xml') {
            it.filter {String line -> line.replaceAll('\\$\\{environment\\}', environment) }
        }
    }
}

tomcat.contextPath = '/'

repositories {
    mavenCentral()
}

configurations {
    runtimeOnly
}

dependencies {
    testCompile "junit:junit:4.11",
                "org.mockito:mockito-core:1.9.5"

    compile "org.springframework:spring-webmvc:4.1.6.RELEASE",
            "org.springframework:spring-orm:4.1.6.RELEASE",
            "log4j:log4j:1.2.17",

            //database dependencies
            "org.postgresql:postgresql:9.3-1102-jdbc4",
            "org.hibernate:hibernate-validator:5.0.1.Final",
            "org.hibernate:hibernate-core:4.3.6.Final"

    providedCompile "javax.servlet:servlet-api:2.5"

    // to run our App on Heroku
    runtimeOnly "com.github.jsimone:webapp-runner:7.0.57.2"

    def tomcatVersion = '7.0.59'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"

    runtime 'javax.servlet:jstl:1.2'
}

task stage(dependsOn: ["clean", "war", "copyToLib"])

task copyToLib << {
    copy {
        from configurations.runtimeOnly.copy().setTransitive(false)
        into "$buildDir/libs"
        rename { name ->
            def artifacts = configurations.runtimeOnly.resolvedConfiguration.resolvedArtifacts
            def artifact = artifacts.find { it.file.name == name }
            "${artifact.name}.${artifact.extension}"
        }
    }
}

task dbInit(type: Exec){
    commandLine 'createdb', '-U', 'postgres', '-p', '5432', 'infotrackdb'
}

task loadTestData(type: Exec){
    commandLine 'psql' , 'infotrackdb', '-f', "${project.rootDir.absolutePath}/src/test/data/AddTestData.sql", '-U', 'postgres', '-p', '5432'
}

flyway {
    def snapUser = System.getenv("SNAP_DB_PG_USER")

    if (snapUser != null){
        url = System.getenv("SNAP_DB_PG_JDBC_URL")
        user = snapUser
        password = System.getenv("SNAP_DB_PG_PASSWORD")
    }
    else {
        url = "jdbc:postgresql://localhost:5432/infotrackdb"
        user = "postgres"
        password = "postgres"
    }
    driver = "org.postgresql.Driver"
    locations = ["filesystem:${project.rootDir.absolutePath}/src/main/resources/db.migration"]
}
